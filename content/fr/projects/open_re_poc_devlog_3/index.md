+++
author = 'Turbo Tartine'
date = '2025-07-15T09:38:25+02:00'
draft = true
title = "OpenRE devlog 3 : Harmonisation des normales"
description = 'devlog 3 du projet OpenRE'
+++
## I. Introduction
Si vous √™tes un lecteur du future et que vous lisez ces develogs d'une traite, vous avez surement la structure des pr√©c√©dents num√©ros en t√™te. Mais si vous les d√©couvrez au fur et √† mesure, un petit rappel me semble appropri√©.

Depuis le d√©but de cette serie nous avons entrepris d'harmoniser les diff√©rents jeux de textures issues de Blender et Godot qui composent nos G-Buffers d√©terministe et interactif. Jusqu'ici nous avons trait√© :
- l'alb√©do
- la profondeur

Pour nous aider dans cette t√¢che, nous avons mis au point un petit outil que nous appelons "l'Oracle" et qui nous permet de comparer les textures 2 √† 2. Le resultat de la comparaison prend la forme d'une image (que nous appelons "prophecie") dans laquelle chaque pixel repr√©sente le degr√© de diff√©rence mesur√© entre les 2 textures source. Il s'interprete comme ceci :
- pixel blanc : La diff√©recne est maximale
- pixel noir : Les sources sont identiques

Aujourd'hui nous allons nous occuper des textures de normales. Et comme √† chaque fois, il y aura :
- une premi√®re partie expliquant comment obtenir les textures avec les 2 logiciels
- une seconde partie pr√©sentant les ajustement effectu√©s pour avoir des textures bien en phase 

## II. G√©n√©ration des textures
Une fois n'est pas coutume, nous allons utiliser une nouvelle passe Cycle d√©di√©e c√¥t√© d√©terministe, et une [Render target](/pages/glossary/#render-target) avec un [post-process](/pages/glossary/#post-process) c√¥t√© interactif. 

### 1. Normal int√©ractive
Commen√ßons tout dessuite avec le post-process. Cette fois, la texture qui nous int√©resse est la `hint_normal_roughness_texture`. Voici le code complet :

```glsl
shader_type spatial;
render_mode unshaded, fog_disabled;

uniform sampler2D depth_texture : hint_normal_roughness_texture, repeat_disable, filter_nearest;

void vertex() {
	POSITION = vec4(VERTEX.xy, 1.0, 1.0);
}

void fragment() {
	vec3 normal = texture(normal_roughness_texture, SCREEN_UV.xy).xyz;
	ALBEDO = normal;
}
```

Rien de particulier ici. On ignore simplement la roughness stoqu√© dans le composant `w` de la `hint_normal_roughness_texture` parce qu'on en a pas besoin.

### 2. Normal d√©terministe
La passe √† activer est ici assez evidente. Elle s'appel tout simplement : `Normal`. Vous commencez √† avoir l'habitude maintenant :
- On active la passe `Normal` depuis le panneau lat√©ral
- On ajoute un pin `normal` au noeud `File Output` du `Compositor`
- On relie les 2 et on appuie sur `F12` pour g√©n√©rer le rendu

[![Capture d'√©cran montrant comment activer la normal pass de Cycles](images/active_normal_pass.opti.webp)](images/active_normal_pass.opti.webp)

A ma grande surprise, l'image obtenue ressemble √† √ßa :

[![Capture montrant le resultat bugg√© de l'export de la normal pass de blender](images/normal0002_raw.opti.webp)](images/normal0002_raw.opti.webp)

Ce n'est evidament pas ce qu'on veut. Je pense qu'il s'agit d'un bug de Blender car le contournement est pour le moins suspect :

[![Capture d'√©cran montrant comment fixer le bug d'export de la normal pass](images/fix_blend_bug.opti.webp)](images/fix_blend_bug.opti.webp)

Il suffit d'ajouter un noeud qui ne fait rien du tout entre les 2 pins. Par exemple le noeud `Add` dans l'illistration ci-dessus qui ajoute √† la normal la couleur noir (0, 0, 0). Lorsqu'on fait √ßa, le rendu redevient coh√©rent :

[![Capture montrant la normal pass fix√©e](images/raw_d_normal.opti.webp)](images/raw_d_normal.opti.webp)

## III. R√©glages
Cette fois encore, il a fallu modifier l'oracle pour qu'il supporte la comparaison des textures de normales. Sans entrer dans les d√©tails, j'ai ajout√© une fonction de pr√©-traitement pour chacune des textures :

```glsl
vec3 pre_process_i_normal(vec3 i_normal) {
	return i_normal;
}

vec3 pre_process_d_normal(vec3 d_normal) {
	return d_normal;
}
```

Notez √©galement que nous allons traiter des directions. On va donc changer notre crit√®re de comparaison habituel (distance euclidiene) et utiliser quelque chose de plus pertinant dans ce contexte : l'angle entre les vecteur (que l'on projetera sur [0, 1] en le divisant par œÄ).

```glsl
vec3 compute_normal_difference(vec3 d_frag, vec3 i_frag) {
	float angle = acos(dot(d_frag, i_frag)) / PI;
	return vec3(angle, angle, angle);
}
```
### 1. Normal Packing / Unpacking
Si on compare les textures en l'√©tat, on remarque que les couleurs sont tr√®s diff√©rentes, mais pas seulement. Si on observe la texture d√©terministe, on constate aussi que :
- certaines face sont noir
- les faces opos√©es √† un face noir sont syst√©matiquement color√©es

[![Comparaison c√¥te √† c√¥te des textures de normales interactive et d√©terministe](images/first_compare_det_int.opti.webp)](images/first_compare_det_int.opti.webp)

Quand on y r√©fl√©chi c'est parfaitement normal car contrairement √† une couleur, un vecteur peut avoir des composant n√©gatifs. Lorsqu'on essai de les visualiser, ces derniers sont clamp√©s √† 0 et un vecteur n'ayant que des valeurs n√©gatives apparait donc noir.

Non seulement ce n'est pas pratique pour la visualisation mais surtout la plupart des formats d'images ne permetent pas d'encoder des valeures n√©gatives. Ici ce n'est pas le cas car on utilise EXR, mais avec un autre format, les valeurs seraient clamp√©e √† l'export et la donn√©e serait tout simplement perdu.

La solution usuelle √† ce probleme est de projeter les composants des normales de l'interval [-1, 1] vers l'interval [0, 1] avant d'exporter. On appel ce proc√©d√© le "normal packing". Par la suite, pour retrouver nos normales et faire nos calculs dans le shader apr√®s import, il suffit d'effectuer l'op√©ration inverse ("normal unpacking").

```glsl
// ... Normal generation ...

vec3 packed_normal = (normal + 1.0) / 2.0;

// ... Export ...

// ... Import ...

vec3 unpacked_normal = packed_normal * 2.0 - 1.0;

// ... Shader computations ...

```

Nous avons donc une texture d√©terministe "unpacked" et une texture int√©ractive "packed". Ce qui nous arrangerait, c'est que les 2 soient "packed" pour la visualisation, et "unpacked" pour le calcule de diff√©rence. On va donc packer la d√©terministe dans `pre_process_d_normal` :

```glsl
vec3 pre_process_d_normal(vec3 d_normal) {
	return (d_normal + 1.0) / 2.0;	// Pack
}
```

Et d√©packer les 2 avant le calcule de diff√©rence dans `compute_normal_difference` :

```glsl
vec3 compute_normal_difference(vec3 d_frag, vec3 i_frag) {
	d_frag = d_frag * 2.0 - 1.0;	// Unpack (previously packed in pre_process_d_normal())
	i_frag = i_frag * 2.0 - 1.0;	// Unpack
	
	float angle = acos(dot(d_frag, i_frag)) / PI;
	return vec3(angle, angle, angle);
}
```

Les couleurs sont toujours aux fraises, mais on a fait un premier pas. Et surtout, on peut maintenant visualiser correctement la texture d√©terministe sans avoir √† deviner ce qu'il se passe dans les zones noires.

[![Comparaison c√¥te √† c√¥te des textures de normales interactive et d√©terministe (pack√©e)](images/packed_compare_det_int.opti.webp)](images/packed_compare_det_int.opti.webp)

### 2. View vs World
Une question qu'il faut toujours se poser quand on √©crit un shader, c'est : "dans quel espace sont exprim√©es mes donn√©es". En effet il y a 2 √©coles :
- faire les calcules en `VIEW_SPACE` (l'espace de la cam√©ra)
- faire les calcules en `WORLD_SPACE` (l'espace de la sc√®ne)

Les 2 options sont parfaitement viables, mais √©videment il faut choisir et s'assurer que toutes les donn√©es soient bien conforme √† ce choix (et les changer d'espace si ce n'est pas le cas). Il est donc primordial de savoir dans quel espace Blender et Godot expriment leurs normales.

J'ai √©t√© surpris de ne pas trouver l'info dans leurs documentations respectives. Mais heureusement ce n'est pas tr√®s difficile √† d√©terminer. Il suffit de faire varier l'angle de la cam√©ra et d'observer les couleurs qui repr√©sentent les normales :
- Si les couleurs varient avec l'angle => on est en `VIEW_SPACE`
- Si les couleurs ne bougent pas => on est en `WORLD_SPACE`

Il se trouve que Blender est en `WORLD_SPACE` et Godot en `VIEW_SPACE`. Je pr√©f√®re travailler en `WORLD_SPACE`, on va donc utiliser `pre_process_i_normal` pour effectuer le changement d'espace c√¥t√© Godot :

```glsl
vec3 pre_process_i_normal(vec3 i_normal, mat4 inv_view_matrix) {	
	i_normal = i_normal * 2.0 - 1.0;	// Unpack for space switch
	i_normal = (inv_view_matrix * vec4(i_normal, 0.0)).xyz;
	return (i_normal + 1.0) / 2.0;		// Repack for visualisation
}
```

Vous noterez qu'on effectue un "unpacking" avant le changement d'espace, ce qui est normal. Mais il ne faut pas oublier de "repack" juste pr√®s pour pouvoir visualiser la texture correctement.

Par ailleurs, si vous vous demandez comment on obtient le parametre `inv_view_matrix` qui nous permet de changer d'espace, c'est tr√®s simple. Godot expose √† ses shaders la matrice `INV_VIEW_MATRIX`. Mais elle n'est accessible que depuis `void fragment()` (le main du fragment shader dans le langage de shading de Godot). Il faut donc la passer en parametre.

Obervons maintenant la diff√©rence entre les 2 espaces :

[![Comparaison anim√©e des textures d√©terministes en world space et view space](images/compare_view_world.gif)](images/compare_view_world.gif)

Elle est visible mais pas tr√®s prononc√©e. Cela s'explique par le fait que notre cam√©ra est presque align√©e avec le rep√®re. Dans ce cas `VIEW_SPACE` et `WORLD_SPACE` sont tr√®s proches vis √† vis de la rotation. Cela aurait √©t√© beaucoup plus flagrant si la cam√©ra regardait dans une autre direction. Cela illustre bien l'importance d'avoir plusieurs sc√®nes de test. Certaines diff√©rences √©tant difficiles √† d√©tecter si on se place dans des cas particuliers sans s'en rendre compte.

### 3. Permutations du rep√®re
Evidement Blender et Godot n'utilisent pas le m√™me rep√®re. Le "up vector" de Godot est l'axe Y alors que celui de Blender est l'axe Z. C'est la raisons pour laquelle les couleurs repr√©sentant les normales ne match pas : les cannaux sont en quelques sortes invers√©s. Il faut donc reorganiser tout √ßa dans `pre_process_d_normal`. En comparant les gizmos des 2 logiciels, je suis arriv√© √† la conclusion que la bonne permutation √©tait la suivante :

<img alt="Illustration montrant les rep√®res des 2 logiciels de part et d'autre d'un fl√™che horizontal annot√©e '(x, y, z) => (x, z, -y) ?'" src="./images/blenderToGodot.opti.webp" style="display: block; margin-left: auto; margin-right: auto;" /> 

```glsl
vec3 pre_process_d_normal(vec3 d_normal) {
	d_normal = vec3(d_normal.x, d_normal.z, -d_normal.y);	// Permut
	return (d_normal + 1.0) / 2.0;							// Pack
}
```

Malheureusement, l'Oracle n'avait pas l'aire d'accord. En effet, sur la proph√©cie, on peut voir que le sol et le mur gauche de la cornel-box ne sont pas noir, indiquant que les vecteurs up et right ne sont pas en phase.

[![1er prophecie de l'oracle. Le mur gauche ainsi que le sol sont tr√® claires](images/no_normalize.opti.webp)](images/no_normalize.opti.webp)

Ma vie √©tant visiblement un mensonge, j'ai essay√© d'autres permutations un peu au hasard. Vous savez ce moment o√π on ne comprend pas son erreur et o√π on commence √† changer un signes par ci ... inverser des termes par l√† ... (je sens d'ici les regard desaprobateurs mais je sais que vous le faites aussi üòÖ). 

Comme souvent √ßa ne m'a pas men√© bien loin. Mais √ßa ne veut pas dire que "le random programming" c'est mal en sois. Il faut juste le faire bien ! C'est √† dire guider pas un raisonnement et pas par la fl√®me.

Il n'y a que 48 permutations possibles. C'est pas la mer √† boire. Si je suis capable de reconnaitre la bonne √† coup s√ªr, j'ai juste besoin d'une moulinette pour it√©rer rapidement sur toutes les entr√©es. Cette moulinette la voici :

```glsl
// Applique une permutation parmis les 48 possible
vec3 _dbg_normal_permut(vec3 normal_src, int permut_idx) {
	switch(permut_idx) {
		case 0 : return vec3(normal_src.x, normal_src.y, normal_src.z);
		case 1: return vec3(normal_src.y, normal_src.z, normal_src.x);
		case 2: return vec3(normal_src.z, normal_src.x, normal_src.y);
		case 3: return vec3(normal_src.z, normal_src.y, normal_src.x);
		case 4: return vec3(normal_src.y, normal_src.x, normal_src.z);
		case 5: return vec3(normal_src.x, normal_src.z, normal_src.y);
		
		// ...
		
		case 42: return vec3(-normal_src.x, -normal_src.y, -normal_src.z);
		case 43: return vec3(-normal_src.y, -normal_src.z, -normal_src.x);
		case 44: return vec3(-normal_src.z, -normal_src.x, -normal_src.y);
		case 45: return vec3(-normal_src.z, -normal_src.y, -normal_src.x);
		case 46: return vec3(-normal_src.y, -normal_src.x, -normal_src.z);
		case 47: return vec3(-normal_src.x, -normal_src.z, -normal_src.y);
	}
}

// Uniform permetant de selectionner la permutation au runtime
uniform int dbg_permut_idx = 0;

vec3 pre_process_d_normal(vec3 d_normal) {
	// Application de la permutation selectionn√©e
	d_normal = _dbg_normal_permut(d_normal, dbg_permut_idx);
	return (d_normal + 1.0) / 2.0;		// Pack
}
```

A partir de l√† il suffit de faire play, de r√©gler le `visualisation_mode` de l'oracle sur `D_TEXTURE_ONLY` et d'incr√©menter `dbg_permut_idx` dans l'√©diteur jusqu'√† ce que l'image soit noir.

[![Capture d'√©cran montrant comment fixer le bug d'export de la normal pass](images/all_permuts.webp)](images/all_permuts.webp)

Bon d'accord l'image 11 n'est pas completement noire, mais elle sort quand m√™me bien du lot :
- Elle est globalement plus sombre
- C'est la seule √† n'avoir aucune face completement blanche
- Elle ressemble quand m√™me beaucoup √† la premi√®re permutation qu'on a essay√© ... c'est quoi la formule ? ...

```glsl
// Applique une permutation parmis les 48 possible
vec3 _dbg_normal_permut(vec3 normal_src, int permut_idx) {
	switch(permut_idx) {
		// ...
		
		case 11: return vec3(normal_src.x, normal_src.z, -normal_src.y);

		// ...
	}
}
```

Ok... c'est exactement la m√™me en faite... Maintenant qu'on a compar√© l'entr√©e avec toutes les autres, √ßa parait evident : on √©tait bon depuis le d√©but. Le probl√®me ne venait pas d'une erreur de permutation des axes. C'√©tait autre chose.

### 4. Normalisation
J'ai mis un moment √† r√©soudre l'√©nigme. J'ai m√™me envisag√© l'id√©e de resortir la carte "Good Enough". Apr√®s tout le pr√©sage n'est pas bien pire que celui qu'on avait acc√©pt√© pour l'alb√©do et quand on compare les textures actuelles, elle sont quand m√™me tr√®s proche.

[![Comparaison anim√©e des textures interactive et d√©terministe en l'√©tat](images/no_normalize_comp.gif)](images/no_normalize_comp.gif)

Mais dans le cas des normales on ne peut pas se contanter d'un jug√© √† l'oeuil. On les repr√©sente par des couleurs pour pouvoir le visualiser facilement mais il faut garder √† l'esprit que ce sont en r√©alit√© des donn√©es math√©matique qui interviennent dans des calculs complexes. Le risque d'accumuler de l'erreur au fils des op√©rations est trop grand. Il faut faire mieux !

Le soucis √©taitait en fait tout b√™te. Pour calculer l'angle entre les vecteurs dans `compute_normal_difference` la formule devrait √™tre : 
```glsl 
float angle = acos(dot(v1, v2)) / (length(v1) * length(v2));
```

Mais dans notre cas, les vecteurs sont des normales. Leur norme est donc en principe √©gale √† 1. Le d√©nominteur peut donc √™tre ignor√© c'est pourquoi j'utilise la version simplifi√©e :
```glsl 
float angle = acos(dot(v1, v2));
```

J'igniore pourquoi, mais il s'av√®re que les normales fournies par Blender et Godot ne sont pas toujours √©gales √† 1. Pour le mettre en √©vidence, j'ai temporairement modifi√© les fonctions `pre_process_i_normal` et `pre_process_d_normal` de mani√®re √† ce que l'on puisse visualiser l'erreur grossie 100 fois :

```glsl 
vec3 pre_process_i_normal(vec3 i_normal, mat4 inv_view_matrix) {
	i_normal = i_normal * 2.0 - 1.0;	// Unpack
	float norm_err = (1.0 - length(i_normal)) * 100.0;
	return vec3(norm_err, norm_err, norm_err);
}

vec3 pre_process_d_normal(vec3 d_normal) {	
	float norm_err = (1.0 - length(d_normal)) * 100.0;
	return vec3(norm_err, norm_err, norm_err);
}
```

Le r√©sultat est sans appel. Dans un cas comme dans l'autre, les normales ne sont pas toujours des vecteurs unitaires :

[![Comparaison c√¥te √† c√¥te des erreures de taille de normal grossies 100x. A gauche, blender est mauvais principalement sur les angles. A droite godot est bruit√© sur les surface courbe et sur les murs de gauche et le sol produites](images/err_comp.opti.webp)](images/err_comp.opti.webp)

Si on normalise les vecteurs avant le calcule de l'angle, on obtien un pr√©sage de bien meilleur qualit√© :

```glsl 
vec3 compute_normal_difference(vec3 d_frag, vec3 i_frag) {
	d_frag = d_frag * 2.0 - 1.0;	// Unpack
	i_frag = i_frag * 2.0 - 1.0;	// Unpack
	
	d_frag = normalize(d_frag);		// Fix norm err
	i_frag = normalize(i_frag);		// Fix norm err
	
	float angle = acos(dot(d_frag, i_frag)) / PI;
	return vec3(angle, angle, angle);
}
```

[![2nd prophecie de l'oracle. Tout est bien noir √† l'exception des contours. On voit apparaitre de l√©gers paternes semblant suivre la g√©om√©trie des faces sur les surfaces courbes](images/normalize.PNG)](images/normalize.PNG)

On notera la pr√©sence de pat√®rnes sur les surface courbes. Les motifs semble suivrent les faces qui composent la g√©om√©trie. Je pense que √ßa vient de la fa√ßon dont les 2 logiciels calcule les normales aux sommets. C'est toujours plus ou moins une moyenne des normales des faces adjacentes, mais il existe plusieurs heuristique pour pond√©rer cette moyenne (prorata des surface, des angles, mix des 2 etc...). Il ont d√ª tout simplement en choisir des diff√©rentes mais ce ne sera pas un probleme pour nous. Si notre bottle-neck est l'heurisique choisie on peut s'arr√©ter l√†. On est bien assez pr√©cis.

## IV. Conclusion 
L'harmonisation des normale aura √©t√© plut√¥t facile. Mis √† part le coup des normes non unitaires et le possible bug de blender, je m'attendais √† peu pr√®s √† touts les r√©glages effectu√©s. Les questions d'espace, de rep√®re ainsi que ces histoires de packing sont en effet assez usuelles en programmation graphique. Ce sont des pi√®ges auxquels on fini par penser naturellement apr√®s √™tre tomb√© dedans 10 fois.

Nous disposons donc desormais d'une calibration satifaisante de nos Blender et Godot pour les maps suivantes :
- alb√©do
- depth
- normales

Ce sont les donn√©es minimales n√©cessaires √† une premi√®re impl√©mentation de la lumi√®re. On va donc pouvoir laisser l'oracle un peu tranquille et commencer √† int√©grer de vrais elements int√©ratifs par dessus un arri√®re plan d√©terministe et voire comment on g√®rer l'illumination dans les 2 sens.

C'est donc dans le prochain num√©ro que les pi√®ces vont enfin commencer √† s'assembler. J'ai hate de trouver un peu de temps pour m'y ateler... alors √† tr√®s vite, je l'esp√®re !